* Алгоритм
Цель: получить на выходе описание вакансии + ссылку на нее

** Пререквизиты
- включить тридактил для файрфокса
- включить в нем hint mode
- определить цвет границ блоков в гимпе

** Библиотеки
#+NAME: libraries
#+BEGIN_SRC lisp :noweb yes
  (ql:quickload "clx")
  (ql:quickload "zpng")
  (ql:quickload "png-read")
  (ql:quickload "local-time")

#+END_SRC

** Скраппинг
#+NAME: define_displays
#+BEGIN_SRC lisp
    (defmacro with-display (host (display screen root-window) &body body)
      `(let* ((,display (xlib:open-display ,host))
              (,screen (first (xlib:display-roots ,display)))
              (,root-window (xlib:screen-root ,screen)))
         (unwind-protect (progn ,@body)
           (xlib:close-display ,display))))

    (defmacro with-default-display ((display &key (force nil)) &body body)
      `(let ((,display (xlib:open-default-display)))
         (unwind-protect
              (unwind-protect
                   ,@body
                (when ,force
                  (xlib:display-force-output ,display)))
           (xlib:close-display ,display))))

    (defmacro with-default-display-force ((display) &body body)
      `(with-default-display (,display :force t) ,@body))

    (defmacro with-default-screen ((screen) &body body)
      (let ((display (gensym)))
        `(with-default-display (,display)
           (let ((,screen (xlib:display-default-screen ,display)))
             ,@body))))

    (defmacro with-default-window ((window) &body body)
      (let ((screen (gensym)))
        `(with-default-screen (,screen)
           (let ((,window (xlib:screen-root ,screen)))
             ,@body))))

    (defun x-size ()
      (with-default-screen (s)
        (values
         (xlib:screen-width s)
         (xlib:screen-height s))))

#+END_SRC

Координаты, по которым располагается описание вакансий у меня
[[TODO:gmm]] как сделать распознавание этой области автоматическим, без
ручного введения ккординат?

#+NAME: define_coordinates
#+BEGIN_SRC lisp
  (defparameter *default-x* 274)
  (defparameter *default-y* 903)
  (defparameter *default-width* (- 1842 903))
  (defparameter *default-heght* (- 1070 274))

#+END_SRC

Инфраструктура:
- создание скриншота
- сохранение скриншота в png по заданному пути
- загрузка png -> на выходе получаем массив пикселей
- превращение цветного изображения в черно-белое

#+NAME: png_preparing
#+BEGIN_SRC lisp
  (defun raw-image->png (data width height)
    (let* ((png (make-instance 'zpng:png :width width :height height
                               :color-type :truecolor-alpha
                               :image-data data))
           (data (zpng:data-array png)))
      (dotimes (y height)
        (dotimes (x width)
          ;; BGR -> RGB, ref code: https://goo.gl/slubfW
          ;; diffs between RGB and BGR: https://goo.gl/si1Ft5
          (rotatef (aref data y x 0) (aref data y x 2))
          (setf (aref data y x 3) 255)))
      png))

  (multiple-value-bind (default-width default-height) (x-size)
    (defun x-snapshot (&key (x *default-x*) (y *default-y*)
                         (width  *default-width*) (height *default-heght*)
                         path)
      ;; "Return RGB data array (The dimensions correspond to the height, width,
      ;; and pixel components, see comments in x-snapsearch for more details),
      ;; or write to file (PNG only), depend on if you provide the path keyword"
      (with-default-window (w)
        (let ((image
               (raw-image->png
                (xlib:get-raw-image w :x x :y y
                                    :width width :height height
                                    :format :z-pixmap)
                width height)))
          (if path
              (let* ((ext (pathname-type path))
                     (path
                      (if ext
                          path
                          (concatenate 'string path ".png")))
                     (png? (or (null ext) (equal ext "png"))))
                (cond
                  (png? (zpng:write-png image path))
                  (t (error "Only PNG file is supported"))))
              (zpng:data-array image))))))

  ;; ;; TEST: save screenshot
  ;; (x-snapshot :path "~/Pictures/snap1.png")


  ;; Ошибка, возникающая когда мы пытаемся прочитать png
  ;; в котором неизвестно сколько байт на точку
  (define-condition unk-png-color-type (error)
    ((color :initarg :color :reader color))
    (:report
     (lambda (condition stream)
       (format stream "Error in LOAD-PNG: unknown color type: ~A"
               (color condition)))))

  (defun load-png (pathname-str)
    "Возвращает массив size-X столбцов по size-Y точек,
     где столбцы идут слева-направо, а точки в них - сверху-вниз
     ----
     В zpng есть указание на возможные варианты COLOR:
     ----
           (defmethod samples-per-pixel (png)
             (ecase (color-type png)
               (:grayscale 1)
               (:truecolor 3)
               (:indexed-color 1)
               (:grayscale-alpha 2)
               (:truecolor-alpha 4)))
    "
    (let* ((png (png-read:read-png-file pathname-str))
           (image-data (png-read:image-data png))
           (color (png-read:colour-type png))
           (dims (cond ((or (equal color :truecolor-alpha)
                            (equal color :truecolor))
                        (list (array-dimension image-data 1)
                              (array-dimension image-data 0)
                              (array-dimension image-data 2)))
                       ((or (equal color :grayscale)
                            (equal color :greyscale))
                        (list (array-dimension image-data 1)
                              (array-dimension image-data 0)))
                       (t (error 'unk-png-color-type :color color))))
           (result ;; меняем размерности X и Y местами
            (make-array dims :element-type '(unsigned-byte 8))))
      ;; (format t "~% new-arr ~A "(array-dimensions result))
      ;; ширина, высота, цвет => высота, ширина, цвет
      (macrolet ((cycle (&body body)
                   `(do ((y 0 (incf y)))
                        ((= y (array-dimension result 0)))
                      (do ((x 0 (incf x)))
                          ((= x (array-dimension result 1)))
                        ,@body))))
        (cond ((or (equal color :truecolor-alpha)
                   (equal color :truecolor))
               (cycle (do ((z 0 (incf z)))
                          ((= z (array-dimension result 2)))
                        (setf (aref result y x z)
                              (aref image-data x y z)))))
              ((or (equal color :grayscale)
                   (equal color :greyscale))
               (cycle (setf (aref result y x)
                            (aref image-data x y))))
              (t (error 'unk-png-color-type :color color)))
        result))
  )
  ;; ;; TEST: equality screenshot and load-file-data
  ;; (assert (equalp (progn
  ;;                   (x-snapshot :path "~/Pictures/snap2.png")
  ;;                   (load-png "~/Pictures/snap2.png"))
  ;;                 (x-snapshot)))


  (defun save-png (width height pathname-str image
                   &optional (color-type :truecolor-alpha))
    (let* ((png (make-instance 'zpng:png :width width :height height
                               :color-type color-type))
           (vector (make-array ;; displaced vector - need copy for save
                    (* height width (zpng:samples-per-pixel png))
                    :displaced-to image :element-type '(unsigned-byte 8))))
      ;; Тут применен потенциально опасный трюк, когда мы создаем
      ;; объект PNG без данных, а потом добавляем в него данные,
      ;; используя неэкспортируемый writer.
      ;; Это нужно чтобы получить третью размерность массива,
      ;; который мы хотим передать как данные и при этом
      ;; избежать создания для этого временного объекта
      (setf (zpng::%image-data png) (copy-seq vector))
      (zpng:write-png png pathname-str)))


  ;; ;; TEST: saving loaded data
  ;; (let* ((from "~/Pictures/snap2.png")
  ;;        (to   "~/Pictures/snap3.png")
  ;;        (image-data (load-png from)))
  ;;   (destructuring-bind (height width depth)
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data)))

  ;; ;; TEST: saving screenshot data
  ;; (let* ((to   "~/Pictures/snap4.png")
  ;;        (image-data (x-snapshot)))
  ;;   (destructuring-bind (height width depth)
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data)))


  ;; превращает цветное изображение в черно-белое
  (defun binarization (image &optional threshold)
    (let* ((dims (array-dimensions image))
           (new-dims (cond ((equal 3 (length dims))  (butlast dims))
                           ((equal 2 (length dims))  dims)
                           (t (error 'binarization-error))))
           (result (make-array new-dims :element-type '(unsigned-byte 8))))
      (macrolet ((cycle (&body body)
                   `(do ((y 0 (incf y)))
                        ((= y (array-dimension image 0)))
                      (do ((x 0 (incf x)))
                          ((= x (array-dimension image 1)))
                        ,@body))))
        (cond ((equal 3 (length dims))
               (cycle (do ((z 0 (incf z)))
                          ((= z (array-dimension image 2)))
                        (let ((avg (floor (+ (aref image y x 0)
                                             (aref image y x 1)
                                             (aref image y x 2))
                                          3)))
                          (when threshold
                            (if (< threshold avg)
                                (setf avg 255)
                                (setf avg 0)))
                          (setf (aref result y x) avg)))))
              ((equal 2 (length dims))
               (cycle (let ((avg (aref image y x)))
                        (when threshold
                          (if (< threshold avg)
                              (setf avg 255)
                              (setf avg 0)))
                        (setf (aref result y x) avg))))
              (t (error 'binarization-error))))
      result))

  ;; ;; TEST: load file and translate it to grayscale and save
  ;; (let* ((from "~/Pictures/snap4.png")
  ;;        (to   "~/Pictures/snap5.png")
  ;;        (image-data (binarization (load-png from))))
  ;;   (destructuring-bind (height width) ;; NB: no depth!
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data :grayscale))) ;; NB: grayscale!


  ;; ;; TEST: binarize and save screenshot
  ;; (let* ((to   "~/Pictures/snap6.png")
  ;;        (image-data (binarization (x-snapshot) 127))) ;; NEW: threshold!
  ;;   (destructuring-bind (height width) ;; NB: no depth!
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data :grayscale))) ;; NB: grayscale!

  ;; ;; TEST: try to load grayscale image and save it
  ;; (let* ((from "~/Pictures/snap6.png")
  ;;        (to   "~/Pictures/snap7.png")
  ;;        (image-data (load-png from)))
  ;;   (destructuring-bind (height width)
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data :grayscale)))

  ;; ;; TEST: try to load grayscale image, binarize and save it
  ;; (let* ((from "~/Pictures/snap7.png")
  ;;        (to   "~/Pictures/snap8.png")
  ;;        (image-data (binarization (load-png from) 127)))
  ;;   (destructuring-bind (height width) ;; NB: no depth!
  ;;       (array-dimensions image-data)
  ;;     (save-png width height to image-data :grayscale)))
#+END_SRC

** START Сегментация
Известно, что:
- подсказки тридактила - это всегда квадратики с офоном определнного
  цвета и двумя белыми буквами на нем

- блоки с названиями вакансия - это всегда длинные прямоугольники разного
  размера в заивисимости от масштаба

- границы блоков всегда одного цвета

Список правил:
- экран разделяется линией, которая идет сверху вниз и имеет длину 2/3
  высоты экрана

- названия вакансий - это:
  - прямоугольные блоки
  - находятся левее разделительной линии, но не выше нее
  - крупнее относительно других блоков (кроме того, что находится самым
    первым, это название списка вакансий типа "golang developer United
    States)

- название более подробно рассматриваемой вакансии (предпосмотр вакансии
  среди списка)
  - это крупный прямоугольный блок
  - находится справа от разделительной линии
  - находится вверху разделительной линии

Необходимо реализовать:
 - предикат, определяющий, является ли область прямоугольником (на вход
   принимает координаты левого ниженго угла и правого верхнего)

 - функцию, которая ищет разделительную линию (начинается снизу экрана,
   занимает более 2/3 высоты экрана)

 - функцию, которая вычисляет площадь двух прямоугольников и определяет,
   явялется ли первый прямоугольник больше второго. Обе фигуры должны
   быть прямоугольниками.

*** DONE rectangle?
Предикат, определяющий, является ли область прямоугольником (на вход принимает координаты левого ниженго угла и правого верхнего)
#+NAME: is_it_rectangle
#+BEGIN_SRC lisp :noweb yes
  (defun rectangle? (coordinates)
    (if (or (atom coordinates)
            (or (atom (car coordinates))
                (atom (cdr coordinates))))
        nil
        (let* ((down-left-x (caar coordinates))
               (down-left-y (cdar coordinates))
               (upper-right-x (caadr coordinates))
               (upper-right-y (cdadr coordinates))
               (hight (- upper-right-y down-left-y))
               (length (- upper-right-x down-left-x)))
          ;; мы считаем прямоугольником только фигуру, у которой
          ;; длина значительно (в 1.5) превышает высоту (но при этом не является
          ;; горизонтальной линией)
          (and (> hight 0) (> length (* hight 1.5))))))

  (defun test-rectangle?()
    (let ((test-line (list (cons 1 1) (cons 1 100)))
          (test-line2 (list (cons 1 1) (cons 100 1)))
          (test-square (list (cons 1 1) (cons 10 10)))
          (test-rectangle (list (cons 1 1) (cons 100 10))))
      (assert (equal (rectangle? test-line) nil))
      (assert (equal (rectangle? test-line2) nil))
      (assert (equal (rectangle? test-square) nil))
      (assert (equal (rectangle? (cons 1 2)) nil))
      (assert (equal (rectangle? '()) nil))
      (assert (equal (rectangle? nil) nil))
      (assert (equal (rectangle? test-rectangle) t))))

  ;; (test-rectangle?)


  <<is_it_bigger_rectangle>>
#+END_SRC

*** DONE bigger-rectangle?
Предикат, который вычисляет площадь двух прямоугольников и определяет,
явялется ли первый прямоугольник больше второго. Обе фигуры должны
быть прямоугольниками.
#+NAME: is_it_bigger_rectangle
#+BEGIN_SRC lisp :noweb yes
  (defun bigger-rectangle? (coordinates1 coordinates2)
    (if (or (not (rectangle? coordinates1))
            (not (rectangle? coordinates2)))
        nil
        (let* ((down-left-x1 (caar coordinates1))
               (down-left-y1 (cdar coordinates1))
               (upper-right-x1 (caadr coordinates1))
               (upper-right-y1 (cdadr coordinates1))
               (down-left-x2 (caar coordinates2))
               (down-left-y2 (cdar coordinates2))
               (upper-right-x2 (caadr coordinates2))
               (upper-right-y2 (cdadr coordinates2))
               (hight1 (- upper-right-y1 down-left-y1))
               (length1 (- upper-right-x1 down-left-x1))
               (hight2 (- upper-right-y2 down-left-y2))
               (length2 (- upper-right-x2 down-left-x2)))
          (> (* hight1 length1) (* hight2 length2)))))

  (defun test-bigger-rectangle?()
    (let ((test-rectangle-bigger (list (cons 1 1) (cons 100 10)))
          (test-rectangle-smaller (list (cons 1 1) (cons 50 10)))
          (test-line (list (cons 1 1) (cons 1 100)))
          (test-line2 (list (cons 1 1) (cons 100 1))))
      (assert (equal (bigger-rectangle? test-rectangle-bigger test-line) nil))
      (assert (equal (bigger-rectangle? test-rectangle-bigger test-line2) nil))
      (assert (equal (bigger-rectangle? test-rectangle-smaller test-rectangle-bigger) nil))
      (assert (equal (bigger-rectangle? test-rectangle-bigger test-rectangle-smaller) t))))

  ;; (test-bigger-rectangle?)
#+END_SRC

*** START find-border-coordinates
#+NAME: border_color
#+BEGIN_SRC lisp :noweb yes
  (defparameter *r-border* 12)
  (defparameter *g-border* 30)
  (defparameter *b-border* 54)

  (defparameter *color-tolerance* 10)
#+END_SRC

Функция проверяет цвет конкетного пикселя в изображении с учетом
возможной цветовой погрешности.
#+NAME: needed_pix?
#+BEGIN_SRC lisp :noweb yes
  <<border_color>>

  (defun needed-pix? (x y r g b array-png)
    (let* ((max-array-y (- (array-dimension array-png 0) 1))
           (max-array-x (- (array-dimension array-png 1) 1))
           (if (or (< max-array-x x)
                   (< max-array-y y)
                   (< x 0)
                   (< y 0))
               nil
               (and (or (= r (aref image y x 0))
                        ( <= (abs (- r (aref image y x 0))) *color-tolerance*))
                    (or (= g (aref image y x 1))
                        ( <= (abs (- g (aref image y x 1))) *color-tolerance*))
                    (or (= b (aref image y x 2))
                        ( <= (abs (- b (aref image y x 2))) *color-tolerance*)))))))

  <<find_border_coordinates>>
#+END_SRC

#+NAME: find_border_coordinates
#+BEGIN_SRC lisp :noweb yes
  (defun is-border? (x y max-y minimum-border-length array-png)
    (do ((y y (incf y))
         (border-length 0 (incf border-length)))
        ((or (= y (+ minimum-border-length y))
             (= y max-y)))
      (if (not (needed-pix? x y *r-border* *g-border* *b-border* array-png))
          (if (< border-length minimum-border-length)
              (return nil)
              (return t))))
    (< border-length minimum-border-length))

  (defun find-border-coordinates(array-png)
    (let* ((max-array-y (- (array-dimension array-png 0) 1))
           (max-array-x (- (array-dimension array-png 1) 1))
           (minimum-border-length (* (floor (/ max-array-y 3))  2)))
      (dotimes (y max-array-y)
        (dotimes (x max-array-x)
          (if (needed-pix? x y *r-border* *g-border* *b-border* array-png)
              (if (is-border? x y max-array-y minimum-border-length array-png)
                  (return (cons x y))))))))

#+END_SRC

** Запуск тестов
Запускает все тесты
#+NAME: run_tests
#+BEGIN_SRC lisp :noweb yes
  (defun run-tests()
    (test-rectangle?)
    (test-bigger-rectangle?))

  ;; (run-tests)
#+END_SRC

** Эмуляция нажатия клавиш и движения мышкой
Честно стащено отсюда https://github.com/0xBECEDA/cl-dino/blob/master/cl-autogui.lisp

Клаиватурные коды могут отличаться.
Чтоб узнать свои для linux ubuntu можно использовать:
https://www.geeksforgeeks.org/showkey-command-in-linux-with-examples/
http://xahlee.info/linux/linux_x11_mouse_button_number.html
#+BEGIN_SRC bash
  sudo showkey # показывает коды клавы
  xev # показывает коды кнопок мыши
#+END_SRC

#+NAME: keyboard_simulation
#+BEGIN_SRC lisp
  (defun mklist (obj)
    (if (and
         (listp obj)
         (not (null obj)))
        obj (list obj)))

  (defmacro defun-with-actions (name params actions &body body)
    "This macro defun a function which witch do mouse or keyboard actions,
  body is called on each action."
    `(defun ,name ,params
       (mapcar
        #'(lambda (action)
            ,@body)
        (mklist ,actions))))

  (defun x-move (x y)
    (if (and (integerp x) (integerp y))
        (with-default-display-force (d)
          (xlib/xtest:fake-motion-event d x y))
        (error "Integer only for position, (x: ~S, y: ~S)" x y)))

  (defun perform-mouse-action (press? button &key x y)
    (and x y (x-move x y))
    (with-default-display-force (d)
      (xlib/xtest:fake-button-event d button press?)))

  (macrolet ((def (name actions)
               `(defun-with-actions ,name
                    ;; у меня клик левой кнопкой мыши - это button 1
                    (&key (button 1) x y)
                    ,actions
                  (funcall #'perform-mouse-action
                           action button :x x :y y))))
    (def x-mouse-down t)
    (def x-mouse-up nil)
    (def x-click '(t nil))
    (def x-dbclick '(t nil t nil)))

  (defmacro with-scroll (pos neg clicks x y)
    `(let ((button (cond
                     ((= 0 ,clicks) nil)
                     ((> 0 ,clicks) ,pos) ; scroll up/right
                     ((< 0 ,clicks) ,neg)))) ; scroll down/left
       (dotimes (_ (abs ,clicks))
         (x-click :button button :x ,x :y ,y))))

  (defun x-vscroll (clicks &key x y)
    (with-scroll 4 5 clicks x y))

  (defun x-scroll (clicks &key x y)
    (x-vscroll clicks :x x :y y))

  (defun x-hscroll (clicks &key x y)
    (with-scroll 7 6 clicks x y))

  (defun perform-key-action (press? keycode) ; use xev to get keycode
    (with-default-display-force (d)
      (xlib/xtest:fake-key-event d keycode press?)))

  (macrolet ((def (name actions)
               `(defun-with-actions ,name (keycode)
                    ,actions
                  (funcall #'perform-key-action
                           action keycode))))
    (def x-key-down t)
    (def x-key-up nil)
    (def x-press '(t nil)))

#+END_SRC

** Скраппинг + текущая проблема
План работ:
- открыть окно браузера
- кликнуть на первую вакансию
- сделать скрин правой области экрана (там вакансия) -> прокрутить правую
  область вниз -> сделать скрин

Представлены 2 варианта открытия оконки браузера, которые должны работать
идентичным образом.
Ожидаемое поведение:
- проиcходит эмуляция клика левой кнопкой мыши на заданных
  координатах. Браузер открывается (не новое окно, а то, что уже было
  открыто)

Текущее поведение:
- открывается меню браузера (выйти их всех окон, просмотреть окна и т.д.) или
- открывается новое окно в браузере

Гипотезы:
- левый клик мыши в моем случае - это не button 1. Не подтвердилось. Чтоб
  узнать, какой код какому событию мыши/клавиатуры соответствует, см [[%D0%AD%D0%BC%D1%83%D0%BB%D1%8F%D1%86%D0%B8%D1%8F %D0%BD%D0%B0%D0%B6%D0%B0%D1%82%D0%B8%D1%8F %D0%BA%D0%BB%D0%B0%D0%B2%D0%B8%D1%88 %D0%B8 %D0%B4%D0%B2%D0%B8%D0%B6%D0%B5%D0%BD%D0%B8%D1%8F %D0%BC%D1%8B%D1%88%D0%BA%D0%BE%D0%B9][сюда]]
- событие происходит так быстро, что обрабатывается неправильно - не
  подтвердилось, см закомментированный вариант open-browser
- у меня сбиты настройки - гипотеза проверяется. Материалы с похожей
  проблемой:
https://superuser.com/questions/1590467/clickpad-left-click-not-working-on-ubuntu-20-04-when-touchpad-is-not-touched
https://askubuntu.com/questions/1274816/mouse-frequently-does-effect-of-right-click-when-i-do-single-left-click-on-ubunt
https://askubuntu.com/questions/1271281/dell-precision-7550-physical-mouse-buttons-behaving-like-clickpad


#+NAME: open_browser
#+BEGIN_SRC lisp
  ;; координаты иконки браузера у меня
  (defparameter *default-browser-x* 35)
  (defparameter *default-browser-y* 75)

  ;;(defun open-browser()
  ;;  (x-mouse-down :x *default-browser-x* :y *default-browser-y*)
  ;;  (sleep 0.01)
  ;;  (x-mouse-up :x *default-browser-x* :y *default-browser-y*))

  (defun open-browser()
    (x-click :x *default-browser-x* :y *default-browser-y*))

  ;; (open-browser)
#+END_SRC

* Tangle
   #+NAME: recognition
   #+BEGIN_SRC lisp :tangle code-file.lisp :noweb yes tangle :exports code :padline no :comments none
     <<libraries>>
     <<define_displays>>
     <<define_coordinates>>
     <<png_preparing>>
     <<is_it_rectangle>>
     <<needed_pix?>>
     <<keyboard_simulation>>
     <<open_browser>>
     <<run_tests>>
   #+END_SRC

* Как это запустить
- станглить - получится code-file.lisp
- залить весь код в репл
- выполнить ~(open-browser)~ - описани проблемы с гипотезами и ссылками [[%D0%A1%D0%BA%D1%80%D0%B0%D0%BF%D0%BF%D0%B8%D0%BD%D0%B3 + %D1%82%D0%B5%D0%BA%D1%83%D1%89%D0%B0%D1%8F %D0%BF%D1%80%D0%BE%D0%B1%D0%BB%D0%B5%D0%BC%D0%B0][сюда]]
